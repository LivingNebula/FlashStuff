<?xml version="1.0" encoding="utf-8"?>
<s:Graphic xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx">
	
	<fx:Script>
		<![CDATA[
			import components.GradientLabel;
			
			import mx.events.FlexEvent;
			
			import spark.components.Label;
			
			private var _source:Object;
			private var _payoutID:String;
			private var _payouts:Array;
			private var _flashPayouts:Array = [];
			private var _title:String;
			private var _fontFamily:String;
			private var _fontSize:Number;
			private var _fontColors:Array = [];
			private var _timerRef:uint = 0;
			private var _labels:Array = [];
			
			// Initializes the Graphic
			public function initGraphic( source:Object, fontFamily:String, fontSize:Number, fontColors:Array, title:String, payoutID:String, payouts:Array ):void
			{		
				setSource( source );							
				setFontFamily( fontFamily );				
				setFontSize( fontSize );
				setFontColors( fontColors );
				setTitle( title );
				setPayoutID( payoutID );
				setPayouts( payouts );
			}
			
			// Sets the source of the Graphic
			private function setSource( value:Object ):void
			{
				_source = value;
				bmpIcon.source = _source;				
			}
			
			// Sets the font family of the Graphic
			private function setFontFamily( value:String ):void
			{
				_fontFamily = value;
				grpText.setStyle( "fontFamily", _fontFamily );
			}
			
			// Sets the font size of the Graphic
			private function setFontSize( value:Number ):void
			{
				_fontSize = value;
				grpText.setStyle( "fontSize", _fontSize );
			}
			
			// Sets the font colors of the Graphic
			private function setFontColors( value:Array ):void
			{
				_fontColors = value;
			}
			
			// Sets the title of the Graphic
			private function setTitle( value:String ):void
			{
				_title = value;
				if( _title != "" )
				{
					var lbl:GradientLabel = new GradientLabel(
						"lblTitle",
						220,
						0,
						_title,
						null,
						0,
						"left",
						_fontColors
					);
					lbl.horizontalCenter = 0;
					lbl.y = 0;
					
					grpText.addElement( lbl );					
				}
			}
			
			// Sets the payout ID of the Graphic
			private function setPayoutID( value:String ):void
			{
				_payoutID = value;				
			}
			
			// Gets the payout ID of the Graphic
			public function getPayoutID():String
			{
				return _payoutID;
			}
			
			// Sets the payouts of the Graphic
			private function setPayouts( payouts:Array ):void
			{				
				// Check if the value was passed in			
				_payouts = payouts;					
				if( _payouts )
				{
					var y:int = _title != "" ? _fontSize : 0;
					bmpIcon.y = y + 15;
					
					for( var i:int = _payouts.length; i > 0; i-- )
					{					
						if( _payouts[i - 1] > 0 )
						{
							var lbl:GradientLabel = new GradientLabel(
								"lblPayout_" + i,
								220,
								0,
								i + "x " + _payouts[i - 1],
								null,
								i == _payouts.length ? _fontSize : _fontSize * 0.8,
								"right",
								_fontColors
							);
							lbl.x = -110;
							lbl.y = y;
							
							y += ( i == _payouts.length ? _fontSize : _fontSize * 0.75 );
							
							_labels.push( lbl );
							grpText.addElement( lbl );
						}
					}
				}
			}
			
			// Starts the flashing of the Graphic
			public function start_Flash( payoutCount:int ):void 
			{ 						
				if( _timerRef == 0 )
				{
					for( var i:int = 0; i < _labels.length; i++ )
					{
						var lbl:GradientLabel = ( _labels[i] as GradientLabel );
						if( lbl.id == "lblPayout_" + payoutCount )
						{
							_timerRef = setInterval( flashWin, 500, lbl );
							break;
						}
					}
				}															
			}
			
			// Flashes the Graphic
			private function flashWin( lbl:GradientLabel ):void
			{				
				lbl.visible = !lbl.visible;
			}
			
			// Stops the flashing of the Graphic
			public function stop_Flash():void
			{
				if( _timerRef )
				{
					clearInterval( _timerRef );
					_timerRef = 0;
					
					for( var i:int = 0; i < _labels.length; i++ )
					{
						_labels[i].visible = true;
					}					
				}						
			}
		]]>
	</fx:Script>
		
	<s:BitmapImage id="bmpIcon" width="104" height="59" x="115" y="15" />				
	<s:Group id="grpText"></s:Group>
	
</s:Graphic>
