<?xml version="1.0" encoding="utf-8"?>
<s:SkinnableContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
					  xmlns:s="library://ns.adobe.com/flex/spark" 
					  xmlns:mx="library://ns.adobe.com/flex/mx" 
					  xmlns:cx="components.*"
					  width="375" height="225" 
					  horizontalCenter="0" verticalCenter="0"
					  creationComplete="init()">

	<fx:Script>
		<![CDATA[
			import assets.Images;
			import assets.SoundManager;
			import assets.Sounds;
			
			import mx.events.FlexEvent;
			
			import services.SweepsAPI;
			
			import utils.DebugHelper;

			// Logging
			private static const logger:DebugHelper = new DebugHelper( RedeemAll );				
			
			private var requestTimer:Timer;
			private var requestInProcess:Boolean;
			
			// Initializes the control
			private function init():void
			{
				// Log Activity
				logger.pushContext( "init", arguments );
				
				// Add a keyboard listener
				addEventListener( KeyboardEvent.KEY_UP, keyHandler );
				
				// Initialize the long request indicator
				initIndicator();
				
				// Replace "Redeem" text if Donate mode enabled
				if( Sweeps.DonateEnabled )
				{
					txtRedeem.text = "DONATE";
					btnRedeem.label = "Donate All";
				}
				
				// Set the page focus
				btnRedeem.setFocus();
				
				// Clear Context
				logger.popContext();				
			}													
			
			// Initializes the long request indicator control
			private function initIndicator():void
			{
				// Log Activity
				logger.pushContext( "initIndicator", arguments );
				
				// Initialize the long request indicator
				var loader:MovieClip = MovieClip( new assets.Images.loaderSWF() );
				loader.x = ( this.width - 220 ) / 2;
				
				var holder:SpriteUIComponent = new SpriteUIComponent( loader );
				holder.horizontalCenter = holder.verticalCenter = 0;
				holder.width = this.width;
				holder.height = this.height;							
				
				grpProcessing.addElement( holder );
				
				// Clear Context
				logger.popContext();				
			}
			
			// Set the scale of the control
			public function setScale( scale:Number ):void
			{
				scaleY = scaleX = scale;
				x = stage.stageWidth / 2 - ( width * scale ) / 2;
				y = stage.stageHeight / 2 - ( height * scale ) / 2;				
			}
			
			// Handles the click event of the "Redeem" button
			protected function btnRedeem_clickHandler( event:MouseEvent ):void
			{
				// Log Activity
				logger.pushContext( "btnRedeem_clickHandler", arguments );
				
				processRedeem(); // Process the redeem request
				
				// Clear Context
				logger.popContext();				
			}
			
			// Handles the click event of the "Cancel" button
			protected function btnCancel_clickHandler( event:MouseEvent ):void
			{
				// Log Activity
				logger.pushContext( "btnCancel_clickHandler", arguments );
				
				// Remove the keyboard listener
				removeEventListener( KeyboardEvent.KEY_UP, keyHandler );
				
				// Dispatch a redeem response event
				Sweeps.getInstance().closeRedeemAll( Sweeps.Entries, Sweeps.Winnings );
				
				this.visible = false; // Hide the current control
				
				// Clear Context
				logger.popContext();				
			}																				
			
			// Handles the mouse down event of the button
			protected function mouseDownHandler( event:MouseEvent ):void
			{		
				SoundManager.playSound( assets.Sounds["buttonClick"], 0, 0 ); // Play the button click sound
			}																		
			
			// Processes the redeem request
			private function processRedeem():void
			{
				// Log Activity
				logger.pushContext( "processRedeem", arguments );
				
				if( isValid() )
				{		
					var username:String = Sweeps.Username;
					var password:String = Sweeps.Password;
					var redeemAmount:int = Sweeps.Winnings;
					
					// Remove the keyboard listener
					removeEventListener( KeyboardEvent.KEY_UP, keyHandler );
					
					if( redeemAmount > 0 ) // Check if the redeem amount is valid
					{
						// Disable the control's buttons
						toggleControls( false );
						
						// Mark the request as in process
						requestInProcess = true;
						
						// Initialize timer in case request takes longer than expected
						requestTimer = new Timer( 1000, 1 );
						requestTimer.addEventListener( TimerEvent.TIMER, requestTimer_Listener );
						requestTimer.start();
						
						// Send the request to the service API and check status
						SweepsAPI.redeemEntries( username, password, Sweeps.Entries, Sweeps.Winnings, redeemAmount, checkRedeem, handleRedeemError );
					}
					else
					{
						// Dispatch a redeem response event
						Sweeps.getInstance().closeRedeemAll( Sweeps.Entries, Sweeps.Winnings );
					}
				}
				else 
				{
					lblError.text = "Unable to process request.";
				}
				
				// Clear Context
				logger.popContext();				
			}
			
			// Checks if the parameters are valid
			private function isValid():Boolean
			{
				if( Sweeps.Winnings == 0 ) { return false; }
				
				lblError.text = "";
				return true;
			}
			
			// Checks the status of the redeem
			private function checkRedeem( entries:int, winnings:int ):void
			{
				// Log Activity
				logger.pushContext( "checkRedeem", arguments );
				
				// Enable the control's buttons
				toggleControls( true );
				
				// Mark the request as processed
				requestInProcess = false;
				
				// Remove the request timer
				if( requestTimer != null )
				{
					requestTimer.removeEventListener( TimerEvent.TIMER, requestTimer_Listener );
					requestTimer.stop();
					requestTimer = null;
				}
				
				// Hide the long request indicator and show the redeem options 
				lblWait.visible = lblWait.includeInLayout = false;
				grpProcessing.visible = grpProcessing.includeInLayout = false;
				vgEntry.visible = vgEntry.includeInLayout = true;
				
				// Dispatch a redeem response event
				Sweeps.getInstance().closeRedeemAll( entries, winnings );
				
				// Clear Context
				logger.popContext();				
			}
			
			// Handles redeem errors
			private function handleRedeemError( errorCode:int, error:String ):void
			{
				// Log Activity
				logger.pushContext( "handleRedeemError" ).error.apply( null, arguments );
				
				// Enable the control's buttons
				toggleControls( true );
				
				// Mark the request as processed
				requestInProcess = false;
				
				// Remove the request timer
				if( requestTimer != null )
				{
					requestTimer.removeEventListener( TimerEvent.TIMER, requestTimer_Listener );
					requestTimer.stop();
					requestTimer = null;
				}
				
				// Hide the long request indicator and show the redeem options 
				lblWait.visible = lblWait.includeInLayout = false;
				grpProcessing.visible = grpProcessing.includeInLayout = false;
				vgEntry.visible = vgEntry.includeInLayout = true;
				
				var pTitle:String;
				var pMessage:String;
				var pIsLogout:Boolean = false;
				var pIsError:Boolean = false;
				
				switch( errorCode )
				{
					case SweepsAPI.ERROR_CODE_UNAUTHORIZED:
						pTitle = "Unauthorized";
						pMessage = "We're sorry, but your account can only be logged in to one computer at a time.";
						pIsLogout = true;
						break;
					
					case SweepsAPI.ERROR_CODE_INSUFFICIENT_ENTRIES:
						pTitle = "Insufficient Entries";
						pMessage = "We're sorry, but it appears your entries were out of sync with the server.\n\nWe have updated your balances and you may continue playing.";
						pIsError = true;
						break;					
					
					default:
						pTitle = "Oops!";
						pMessage = "We're sorry, but there was an issue while trying to complete this request.\n\nPlease try again.";
						pIsError = true;
						break;
				}
				
				// Display a popup indicating an error occurred
				Sweeps.getInstance().createPopUp( pTitle, pMessage, pIsError, pIsLogout );
				
				// Clear Context
				logger.popContext();				
			}			
			
			// Enables/Disables the user controls
			private function toggleControls( enable:Boolean ):void
			{
				// Log Activity
				logger.pushContext( "toggleControls", arguments );
				
				btnRedeem.enabled = enable;
				btnCancel.enabled = enable;
				
				// Clear Context
				logger.popContext();				
			}
			
			// Displays an indicator in the event that the request takes longer than expected
			private function requestTimer_Listener( event:TimerEvent ):void 
			{
				// Log Activity
				logger.pushContext( "requestTimer_Listener", arguments );
				
				// Remove the request timer
				requestTimer.removeEventListener( TimerEvent.TIMER, requestTimer_Listener );
				requestTimer.stop();
				requestTimer = null;
				
				// Check if the request is still in process
				if( requestInProcess )
				{
					lblWait.visible = lblWait.includeInLayout = true;
					grpProcessing.visible = grpProcessing.includeInLayout = true;
					vgEntry.visible = vgEntry.includeInLayout = false;					
				}
				
				// Clear Context
				logger.popContext();				
			}
			
			// Handles the keyboard 'Key Up' event
			private function keyHandler( event:KeyboardEvent ):void 
			{
				switch( event.keyCode )
				{
					case Keyboard.ENTER:
						processRedeem(); // Process the redeem request
						break;									
				}				
			}					
		]]>
	</fx:Script>
	
	<s:BitmapImage source="{Images.messageBox}"></s:BitmapImage>
	<s:Group width="100%" height="100%" horizontalCenter="0">
		<s:layout>
			<s:VerticalLayout horizontalAlign="center"/>
		</s:layout>
		<s:Label id="txtRedeem" text="BUY" styleName="Title" paddingTop="7"></s:Label>
		<s:Label id="lblError" styleName="Error" visible="true" paddingTop="15"></s:Label>
		<s:Label id="lblWait" styleName="Label" text="Processing. Please wait..." paddingTop="25" includeInLayout="false" visible="false"></s:Label>
		<s:Group id="grpProcessing" horizontalCenter="0" verticalCenter="0" minWidth="220" minHeight="9" color="0x0000FF" includeInLayout="false" visible="false"></s:Group>
		<s:VGroup id="vgEntry" horizontalAlign="center" paddingTop="20" gap="30" paddingLeft="0" paddingRight="0">						
			<s:Label id="lblEntries" styleName="Label" text="Do you wish to convert your winnings to entries?"></s:Label>
			<cx:ImageButton id="btnRedeem" skinClass="skins.ButtonSkin" label="Buy All" imageSkin="{Images.btnLarge_up}" imageSkinOver="{Images.btnLarge_over}" imageSkinDown="{Images.btnLarge_down}" imageSkinDisabled="{Images.btnLarge_disabled}" click="btnRedeem_clickHandler(event)" mouseDown="mouseDownHandler(event)"></cx:ImageButton>
		</s:VGroup>		
	</s:Group>	
	<mx:Button id="btnCancel" skin="{Images.iconCross}" buttonMode="true" top="5" right="10" click="btnCancel_clickHandler(event)" mouseDown="mouseDownHandler(event)"></mx:Button>
</s:SkinnableContainer>
